
现在需要将 deps目录下的库暴露给 src目录下的项目使用，那么最佳实践应该怎么做呢？
参考了azerothcore-wotlk 项目的目录结构，以及deps目录下几个依赖文件的CmakeLists.txt， 大致思路如下：

1、 通过接口库定义通用的基础的目标属性

2、 在linux下 使用导入库导入外部安装的依赖库
```cmake
if(UNIX)
  # Look for an installed zlib on unix
  find_package(ZLIB REQUIRED)
  add_library(zlib SHARED IMPORTED GLOBAL)
  set_target_properties(zlib
    PROPERTIES
      IMPORTED_LOCATION
        "${ZLIB_LIBRARIES}"
      INTERFACE_INCLUDE_DIRECTORIES
        "${ZLIB_INCLUDE_DIRS}")
endif ()
```

3、在windows下通过源码编译依赖库

```cmake
SET(zlib_STAT_SRCS
    adler32.c
    compress.c
    crc32.c
    deflate.c
    infback.c
    inffast.c
    inflate.c
    inftrees.c
    trees.c
    uncompr.c
    zutil.c
  )

  add_library(zlib STATIC
    ${zlib_STAT_SRCS})

  set_target_properties(zlib PROPERTIES LINKER_LANGUAGE CXX)

  target_include_directories(zlib
    PUBLIC
      ${CMAKE_CURRENT_SOURCE_DIR})

  target_link_libraries(zlib
    PRIVATE
      acore-dependency-interface)

  set_target_properties(zlib
      PROPERTIES
        FOLDER
          "deps")
```

4、在windows下使用已经编译好的库

```cmake
find_package(Boost ${BOOST_REQUIRED_VERSION} REQUIRED system filesystem program_options iostreams regex)
add_library(boost INTERFACE)

target_link_libraries(boost
        INTERFACE
        ${Boost_LIBRARIES})

target_include_directories(boost
        INTERFACE
        ${Boost_INCLUDE_DIRS})

target_compile_definitions(boost
        INTERFACE
        -DBOOST_DATE_TIME_NO_LIB
        -DBOOST_REGEX_NO_LIB
        -DBOOST_CHRONO_NO_LIB
        -DBOOST_SERIALIZATION_NO_LIB
        -DBOOST_CONFIG_SUPPRESS_OUTDATED_MESSAGE
        -DBOOST_ASIO_NO_DEPRECATED
        -DBOOST_SYSTEM_USE_UTF8
        -DBOOST_BIND_NO_PLACEHOLDERS)
```
先通过find_package 来查找包，找到包以后，再自定义一个interface类型的target， 通过设定该target的一些属性来把依赖关系传递出去。

这里面涉及一个问题：Cmake下所有目标好像都是全局的（这一点还没确认， 但是Cmake下的所有target名字必须唯一似乎应征了这一点）。

**关键点**： Cmake中到底该如何引入外部依赖？

对于轻量级的库，如lua这种非常小的库，直接通过源码引用还是非常好的，这样能避免因为本地工具集与外部lib工具集不一致所造成的各种问题， 也能够灵活的生成
Debug/Release版本的库。

对于重量级的库类， 如Qt 、Boost 等。最好是通过本地安装，使用find_package 查找到相关的包以后，再通过target暴露给给其他项目。


